IMAGE_X dw 0 ;.............................................. RightToLeft x
IMAGE_Y dw 0 ;.............................................. TopToBottom y

IMAGE_LEFT_END dw 0
IMAGE_BOTTOM_END dw 0

;###########################################################

draw_image:
	
	mov 	ax, 0xA000
	mov 	es, ax
	
	
	mov 	bx, word [IMAGE_X]
	push 	bx
	
	mov 	ax, word [gs:si] ;.............................. عرض الصورة
	mov 	word[IMAGE_LEFT_END], bx
	sub 	word[IMAGE_LEFT_END], ax ;...................... الحدود اليسرى للصورة = عرض الصورة + الموقع الأفقي للصورة (س)
	
	mov 	cx, word [IMAGE_Y]
	
	mov 	ax, word [gs:si + 2] ;.......................... ارتفاع الصورة
	mov 	word [IMAGE_BOTTOM_END], cx
	add 	word [IMAGE_BOTTOM_END], ax ;................... الحدود السفلية للصورة = ارتفاع الصورة + الموقع العمودي للصورة (ع)
	
	add 	si, 4 ;......................................... بيانات الصورة تبدأ بعد العرض والارتفاع(كل منها يمثل ثماني مضاعف)
	
	mov 	ax, 320
	mul 	cx
	
	xor 	dx, dx
	
	.draw_pixels_loop:
		
		mov 	dl, byte [gs:si] ;.......................... النقطة التالية (كل نقطة تمثل عن طريق ثماني واحد)
		cmp 	dl, 0 ;..................................... القيمة 0 تعني أن هذه نقطة شفافة، بإمكاننا إما تجاهلها وعدم رسمها أو رسمها باستعمال لون الخلفية
		je 		.ignore
		dec 	dl ;........................................ اللون = قيمة اللون من بيانات الصورة - 1
		
		mov 	di, ax
		add 	di, bx ;.................................... موضع رسم النقطة الحالية = عرض الشاشة(320 نقطة) × الموقع العمودي للصورة + الموقع الأفقي للصورة
		
		mov 	byte [es:di], dl ;.......................... نضع اللون في المكان المناسب على موضع الذاكرة المخصص للشاشعة [0xA000:DI]
		
		.ignore:
		
		inc 	si ;........................................ النقطة التالية
		dec 	bx
		cmp 	bx, word[IMAGE_LEFT_END] ;.................. هل وصلنا إلى نهاية سطر النقاط الخاصة بالصورة؟ (الذي هو نفسه عرض الصورة)
		je 		.next_pixels_row ;.......................... نعم؟ انتقل لرسم سطر النقاط التالي
		jmp 	.draw_pixels_loop ;......................... لا؟ أكمل رسم بقية نقاط السطر
		
	.next_pixels_row:
		
		pop 	bx ;........................................ عرض الصورة
		inc 	cx ;........................................ السطر التالي من النقاط
		cmp 	cx, word[IMAGE_BOTTOM_END] ;................ هل كان هذا أخر سطر من النقاط في الصورة؟
		je 		.return ;................................... نعم؟ إنهاء الرسم
		
		push 	bx ;........................................ حفظ عرض الصورة لرسم بقية الأسطر
		
		mov 	ax, 320
		mul 	cx
		
		jmp 	.draw_pixels_loop ;......................... العودة لرسم بقية نقاط الصورة
		
	.return:
	
		ret

